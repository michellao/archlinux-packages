# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: CupricReki
# Contributor: feschber

pkgname=lan-mouse
pkgver=0.10.0
pkgrel=1
pkgdesc="Software KVM Switch / mouse & keyboard sharing software for Local Area Networks"
arch=('x86_64')
url="https://github.com/feschber/lan-mouse"
license=("GPL-3.0-only")
depends=('libxtst' 'glib2' 'gcc-libs')
makedepends=('cargo')
source=(
  "$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
  fix-kde-modifier.patch
)
sha256sums=('dad750d5e38bc564cb5fff37b21b747d652d363efd292dc57dd69be460c5459e'
            '7b923b1b9a7486b1040e52725baa5e52651da77298023f07c24d2c32762fca67')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
  patch -Np1 < "../fix-kde-modifier.patch"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --frozen --release --no-default-features -F wlroots_emulation,libei_capture,layer_shell_capture,libei_emulation
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --no-default-features -F wlroots_emulation,libei_capture,layer_shell_capture,libei_emulation
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
}

# vim: ts=2 sw=2 et:
